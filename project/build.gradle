apply plugin: 'com.android.application'
apply from: '../config/android_module_shared_config.gradle'

android {
    defaultConfig {
        applicationId = "com.sengami.myastraldiary"
        manifestPlaceholders.applicationName = "My Astral Diary"
        project.archivesBaseName = "my-astral-diary-${versionName}".toString()
    }

    signingConfigs {
        release {
            storeFile file(project.KEYSTORE_PATH)
            storePassword readFile(project.KEYSTORE_PASSWORD_FILE_PATH)
            keyAlias readFile(project.KEYSTORE_ALIAS_FILE_PATH)
            keyPassword readFile(project.KEYSTORE_ALIAS_PASSWORD_FILE_PATH)
            v1SigningEnabled = true
            v2SigningEnabled = true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles fileTree(dir: '../proguard', include: ['*.pro']).asList().toArray()
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
}

dependencies {
    implementation project(":feature:base:data_base")
    implementation project(":feature:base:domain_base")
    implementation project(":feature:base:gui_base")
    implementation project(":feature:splash:gui_splash")
    implementation project(":feature:main:gui_main")
    implementation project(":feature:diary:gui_diary")
    implementation project(":feature:statistics:gui_statistics")
    implementation project(":feature:settings:gui_settings")

    implementation libraries.jetbrainsAnnotations
    implementation libraries.androidXAppCompat
}

String readFile(String filePath) {
    File file = file(filePath)
    if (file.exists()) {
        return file.getText('UTF-8')
    } else {
        println("FILE MISSING: " + filePath)
        return ""
    }
}